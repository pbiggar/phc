{-
 - phc -- the open source PHP compiler
 - See doc/license/README.license for licensing information
 -
 - Definition of the LIR.
 -
 - The LIR represents C code which we generate, and is intended to allow us
 - to easily manipulate it.
 -
 -}

{-
	Configuration
-}

output_dir "src/generated";
prefix "LIR";
external class "Object";
external class "String";
external class "IR::Node";
external class "IR::FOREIGN";
external class "IR::PHP_script";
use list "List";
use string "String";
use namespace "LIR";
no source_rep;

{-
   Top-level structure	
-}

{-
 - To start off, a C file is simply a list of strings. When we add other
 - constructs, we will likely maintain the list of strings for things that we
 - can't, or can't be bothered, maintaining directly.
-}

C_file ::= Piece* ;

Piece ::= Block | UNINTERPRETED ;

Block ::= Statement* ;

Statement ::=    Action				-- (possibly) side-effecting statement 
					| If
					| INTRINSIC			-- Builtin phc function, which we can model.
					| API_CALL			-- Call into the Zend API. We may be able to
											-- model this.
					| CODE				-- Straight-line C code. Can't analyse it, but
											-- we can optimize before and after, while
											-- assuming the C code doesnt change anything.
					;

Action ::=	  Assign_zvp
				| Assign_zvpp
				| Inc_ref
				| Dec_ref
				| Destruct
				| Allocate
				| Clone
--				| Overwrite
				| Separate
				;

If ::=	Cond if_true:Statement* if_false:Statement* ;

Cond ::=		  Is_ref
--				| Equal
--				| Is_copy_on_write
--				| Is_change_on_write
				;

-- zval* lhs = rhs;
Assign_zvp	::= lhs:Zvp rhs:Zvp ;

-- zval** p_lhs = p_rhs;
Assign_zvpp ::= lhs:Zvpp rhs:Zvpp ;

{-
 - Allocators
 -}

-- TODO: model calling the object handlers separately
-- lhs->refcount++;
Inc_ref		::= Zvp ;

-- ALLOC_INIT_ZVAL (lhs);
Allocate		::= Zvp ;
Clone			::= lhs:Zvp rhs:Zvp ;
Separate    ::= Zvpp ;


{-
 - Destructors
 -}

-- lhs->refcount--; // WARNING: this is less safe than Destruct
Dec_ref		::= Zvp ;

-- zval_ptr_dtor (p_lhs);
Destruct		::= Zvpp ;



{-
 - Conditions
 -}

-- lhs->is_ref
Is_ref ::= Zvp;


{-
 - Primitives
 -}

Zvp ::= Deref | ZVP | Null | LITERAL | Uninitialized ;
Zvpp ::= Ref | ZVPP ;

Uninitialized ::= ;
Null ::= ;
Deref ::= Zvpp ;
Ref ::= Zvp ;


{-
 - Extra attributes and methods (mixin code)
 -}

#include <iostream>
#include <sstream>
#include <iomanip>
#include "boost/lexical_cast.hpp"
#include "lib/error.h"
#include "lib/Object.h"
#include "lib/List.h"
#include "lib/String.h"
#include "lib/Boolean.h"
#include "lib/Integer.h"
#include "lib/AttrMap.h"
#include "process_ir/IR.h"
#include "process_lir/LIR_PHP_script_hack.h"

class Node : IR::Node
{
};

class C_file : IR::PHP_script
{
public:
	C_file()
	{
		pieces = new Piece_list;
	}
};
